
# This script can be used by the build-server to merge existing html content with the
# one that was generated by the last build. The Accumulated content will be in the
# EXISTING_HTML_DIR
#
# Arguments:
# EXISTING_HTML_DIR		- The html directory with the existing page on the web-server.
# FRESH_HTML_DIR		- The html directory that was created by the last build.
# ROOT_DIR: 			- The root directory of a CPF project.

include(${CMAKE_CURRENT_LIST_DIR}/../cpfInit.cmake)
include(cpfMiscUtilities)
include(cpfLocations)
include(cpfProjectUtilities)

cpfAssertScriptArgumentDefined(EXISTING_HTML_DIR)
cpfAssertScriptArgumentDefined(FRESH_HTML_DIR)
cpfAssertScriptArgumentDefined(ROOT_DIR)

# Delete directories that are obsolete in the target directory.
file(REMOVE_RECURSE "${EXISTING_HTML_DIR}/${CPF_DOXYGEN_DIR}")
file(REMOVE_RECURSE "${EXISTING_HTML_DIR}/${CPF_CGI_BIN_DIR}/doxysearch.db")
file(REMOVE_RECURSE "${EXISTING_HTML_DIR}/${CPF_OPENCPPCOVERAGE_DIR}")

cpfGetSourcesSubdirectories( packages "${ROOT_DIR}" )
foreach( package ${packages})
	
	# downloads lastbuild
	cpfGetRelLastBuildPackagesDir( dir ${package})
	file(REMOVE_RECURSE "${EXISTING_HTML_DIR}/${dir}")

	# abi/api reports
	cpfGetRelCurrentToLastBuildReportDir( dir ${package} )
	file(REMOVE_RECURSE "${EXISTING_HTML_DIR}/${dir}")
	cpfGetRelCurrentToLastReleaseReportDir( dir ${package})
	file(REMOVE_RECURSE "${EXISTING_HTML_DIR}/${dir}")

endforeach()

# Copy the content of the html directory to target html directory
file(COPY "${FRESH_HTML_DIR}/" DESTINATION "${EXISTING_HTML_DIR}" FILES_MATCHING PATTERN "*" )
